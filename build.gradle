buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "3.+", changing: true
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

apply plugin: 'net.minecraftforge.gradle'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

ext.early = true
apply from: 'project.gradle'

project.ext.package = project.ext.group + '.' + project.ext.projectName.toLowerCase()

if (project.ext.useElytraVersionFormat) {
    def branch
    if (System.env.BRANCH_NAME) {
        // Jenkins support
        branch = System.env.BRANCH_NAME
        branch = branch.substring(branch.lastIndexOf('/') + 1)
    } else {
        branch = 'git rev-parse --abbrev-ref HEAD'.execute().in.text.trim()
    }
    if (branch == "HEAD") {
        branch = 'git rev-parse --short HEAD'.execute().in.text.trim()
    }
    def commits = 'git rev-list --count HEAD'.execute().in.text.trim()
    def dirty = !'git diff-index HEAD'.execute().in.text.trim().isEmpty()
    version = branch + '-' + project.ext.version + '.' + commits + (dirty ? '-dirty' : '')
} else {
    version = project.ext.version
}

group = project.ext.group
archivesBaseName = project.ext.projectName

ext {
    compoundClassesDir = file('build/compound')
}

configurations {
    nonModDep
    shade
}

minecraft {
    mappings channel: 'snapshot', version: project.ext.mappings

    runs {
        client {
            environment "MOD_CLASSES", [
                    sourceSets.main.output.resourcesDir.absolutePath,
                    sourceSets.main.output.classesDirs.asPath,
                    project.compoundClassesDir.absolutePath
            ].join(File.pathSeparator)

            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                mainMod {
                    source sourceSets.main
                }
            }
        }

        server {
            environment "MOD_CLASSES", [
                    sourceSets.main.output.resourcesDir.absolutePath,
                    sourceSets.main.output.classesDirs.asPath,
                    project.compoundClassesDir.absolutePath
            ].join(File.pathSeparator)

            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                mainMod {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.ext.forge}"
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url 'https://repo.tridevmc.com/'
    }
}

def needsShadow = !project.ext.compoundModules.isEmpty()

if (needsShadow) {
    dependencies {
        for (String module : project.ext.compoundModules) {
            def module_identifier = 'com.tridevmc.compound:compound' + '-' + module + ':' + project.ext.compoundVersion
            compile module_identifier
            shade module_identifier
            nonModDep module_identifier
        }
    }

    tasks.build.dependsOn shadowJar

    artifacts {
        archives shadowJar
    }

    task extractNonMod(type: Copy) {
        project.compoundClassesDir.delete()
        project.compoundClassesDir.mkdirs()
        from zipTree(project.configurations.nonModDep.find { it.name.startsWith("compound") })
        into project.compoundClassesDir
    }

    compileJava.dependsOn(extractNonMod)

    shadowJar {
        configurations = [project.configurations.shade]
        relocate 'com.tridevmc.compound', project.ext.projectPackage + '.com.tridevmc.compound'
    }
}

ext.early = false
apply from: 'project.gradle'

if (file('private.gradle').exists()) {
    apply plugin: 'maven-publish'
    apply from: 'private.gradle'
}